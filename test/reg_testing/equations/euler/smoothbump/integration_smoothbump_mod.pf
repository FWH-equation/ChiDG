!--------------------------------------------------------------------------------
!
!                Euler equation set entropy-error regression test
!
!   This test suite runs a series of tests solving the euler equations on a quartic
!   smoothbump grid of varying resolution for varying orders of solution accuracy.
!   At the end of each case, the entropy error is computed for the domain. The flow
!   phenomena that are present for the smoothbump case should be isentropic. The 
!   error metric then, is to compute the entropy generated from the numerical scheme.
!
!   This suite runs the smoothbump case with 6x2x2, 12x4x2, and 24x8x2 grids with
!   quartic elements. Each grid is run from 1st to 4th order accuracy. Each 
!   configuration is run on 1, 2, 7, and 10 processors. The nonlinear convergence
!   for each case is stored in a module global variable that is used to compare
!   convergence.
!
!   After the simulation for each case has been run, an additional test
!   compares the nonlinear convergence of the Newton solver from the parallel
!   simulations to the same case run in serial.
!
!   The boundary condition settings are:
!
!       Inlet:  euler_totalinlet  
!               Total Pressure    = 110000 (Pa) 
!               Total Temperature = 300 (K)
!
!       Outlet: euler_pressureoutlet
!               Static Pressure   = 93000 (Pa)
!
!       Walls: euler_wall
!
!--------------------------------------------------------------------------------
module integration_smoothbump_mod
    use pfunit_mod
    use mod_kinds,          only: rk, ik
    use mod_constants,      only: ZERO, THREE, IO_DESTINATION
    use type_chidg,         only: chidg_t
    use mod_bc,             only: create_bc
    use type_function,      only: function_t
    use type_rvector,       only: rvector_t
    use mod_function,       only: create_function
    use mod_grid_operators, only: initialize_variable
    use mod_entropy,        only: compute_entropy_error

    use mod_chidg_mpi,      only: IRANK, NRANK
    use mpi_f08,            only: MPI_COMM
    use mod_io
    implicit none


    ! Store the nonlinear convergence for each test case. This is used 
    ! to test the nonlinear convergence between a case running on
    ! a different number of procs.
    !
    ! Procs: 1, 2, 4, 7, 10
    !
    type(rvector_t) :: nonlinear_convergence(5,3,4) ! nprocs, ngrids, norders


    public  :: smoothbump




    !>  This parameter initialize the state for each test
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !-------------------------------------------------------------------------------
    @TestParameter
    type, extends(MpiTestParameter) :: case_grid_order
        character(100)  :: gridfile
        integer(ik)     :: order
        real(rk)        :: entropy_ref
    contains
        procedure   :: toString
    end type case_grid_order
    !*******************************************************************************







    !>  Main testing fixture with test components that get initialized for each test
    !!
    !!  @author Nathan A. Wukie 
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !-------------------------------------------------------------------------------
    @TestCase(testParameters={getParameters()}, constructor=newTest)
    type, extends(MpiTestCase) :: smoothbump
        character(100)  :: gridfile
        integer(ik)     :: order
        real(rk)        :: entropy_ref

        type(chidg_t)   :: chidg
        type(mpi_comm)  :: ChiDG_COMM
    contains
        procedure   :: setUp
        procedure   :: tearDown
    end type smoothbump
    !*******************************************************************************



    !> Simple container to hold arbitrary length file names. That way, we can have
    !! an array of filenames that get looped through.
    !!
    !-------------------------------------------------------------------------------
    type :: string
        character(len=:), allocatable   :: str
    end type
    !*******************************************************************************






contains


    !> Parameter constructors
    !!
    !!
    !!
    !------------------------------------------------------------------------------
    function newTest(test_parameter) result(test)
        type(case_grid_order),  intent(in)          :: test_parameter
        type(smoothbump)    :: test

        test%gridfile    = test_parameter%gridfile
        test%order       = test_parameter%order
        test%entropy_ref = test_parameter%entropy_ref

    end function newTest
    !******************************************************************************


    !> Parameter initialization
    !!
    !!
    !!
    !------------------------------------------------------------------------------
    function getParameters() result(params)
        type(case_grid_order), allocatable :: params(:)
    
        type(string),       allocatable :: files(:)
        integer(ik),        allocatable :: orders(:)
        real(rk),           allocatable :: entropies(:)
        integer(ik) :: icase

        files = [  string('smoothbump_6x2x2.h5') , &
                   string('smoothbump_6x2x2.h5') , &
                   string('smoothbump_6x2x2.h5') , &
                   string('smoothbump_6x2x2.h5') , &
                   string('smoothbump_12x4x2.h5'), &
                   string('smoothbump_12x4x2.h5'), &
                   string('smoothbump_12x4x2.h5'), &
                   string('smoothbump_12x4x2.h5'), &
                   string('smoothbump_24x8x2.h5'), &
                   string('smoothbump_24x8x2.h5'), &
                   string('smoothbump_24x8x2.h5'), &
                   string('smoothbump_24x8x2.h5') &
                    ]

        orders = [ 1, &
                   2, &
                   3, &
                   4, &
                   1, &
                   2, &
                   3, &
                   4, &
                   1, &
                   2, &
                   3, &
                   4 ]

        entropies = [ 2.761019163e-3_rk, &
                      3.618440627e-3_rk, &
                      1.116431012e-3_rk, &
                      6.731678976e-4_rk, &
                      3.807878665e-3_rk, &
                      1.323352387e-3_rk, &
                      3.940896088e-4_rk, &
                      8.433932228e-5_rk, &
                      4.038806838e-3_rk, &
                      5.554687475e-4_rk, &
                      5.367783209e-5_rk, &
                      7.223333943e-6_rk ]

        !Initialize the test case container with the parameter
        allocate(params(size(files)))
        do icase = 1,size(files)
            params(icase)%gridfile    = files(icase)%str
            params(icase)%order       = orders(icase)
            params(icase)%entropy_ref = entropies(icase)
        end do

    end function getParameters
    !*******************************************************************************************



    !>  Reporting the parameters of the test
    !!
    !!
    !-------------------------------------------------------------------------------------------
    function toString(this) result(string)
        class (case_grid_order),    intent(in)  :: this
        character(:), allocatable   :: string

        character(len=200) :: buffer

        write(buffer,'( "(gridfile, order)=","(", A10,",", I2, ")" )') this%gridfile, this%order
        string = trim(buffer)

    end function toString
    !********************************************************************************************







    !>  Set up test routine.
    !!
    !!  This routine reads the grid file, sets up boundary conditions, initializes storage
    !!  and sets the appropriate configuration options.
    !!  This routine also gets the numer of processors and the correct MPI communicator for 
    !!  the test.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !!
    !--------------------------------------------------------------------------------------------
    subroutine setUp(this)
        class(smoothbump),  intent(inout)   :: this

        class(function_t),  allocatable :: fcn
        real(rk)                        :: tol, entropy_error

        
        tol = 1.e-6_rk

        !------------------------------------------------------------------------------
        !
        ! Define input data here that is normally read through the namelist input file
        !
        ! As a result, we don't call chidg%init('io')
        !
        !------------------------------------------------------------------------------
        basis            = 'legendre'
        spacedim         = 3
        nterms_s         = this%order**THREE
        gq_rule          = 2
        time_integrator  = 'steady'
        nonlinear_solver = 'quasi_newton'
        linear_solver    = 'fgmres_cgs'
        preconditioner   = 'RASILU0'
        IO_DESTINATION   = 'file'
        !------------------------------------------------------------------------------

        !
        ! Get MPI communicator information from pFUnit
        !
        IRANK                   = this%getProcessRank()
        NRANK                   = this%getNumProcessesRequested()
        this%ChiDG_COMM%mpi_val = this%getMpiCommunicator()



        ! Set timescheme options to pass during initialization
        call toptions%set('dt', 0.1_rk) ! means nothing here
        call toptions%set('nsteps',100)
        call toptions%set('nwrite',0)

        ! Set linear solver options to pass during initialization
        call loptions%set('tol',1.e-8_rk)

        ! Set nonlinear solver options
        call noptions%set('tol',3.e-5_rk)
        call noptions%set('cfl0',1.0_rk)
        call noptions%set('nsteps',100)



        !
        ! Initialize ChiDG environment
        !
        call this%chidg%init('env',this%ChiDG_COMM)


        !
        ! Read grid
        !
        call this%chidg%read_grid(this%gridfile,spacedim)
        call this%chidg%read_boundaryconditions(this%gridfile)



        call this%chidg%initialize_solution_domains(nterms_s)
        call this%chidg%init('communication')
        call this%chidg%init('chimera')
        call this%chidg%initialize_solution_solver()


        !
        ! Set ChiDG components
        !
        call this%chidg%set('time_integrator',  time_integrator,  toptions)
        call this%chidg%set('nonlinear_solver', nonlinear_solver, noptions)
        call this%chidg%set('linear_solver',    linear_solver,    loptions)
        call this%chidg%set('preconditioner',   preconditioner)


        !
        ! Initialize solution
        !
        call create_function(fcn,'constant')

        call fcn%set_option('val',1.13262_rk)
        call initialize_variable(this%chidg%data,1,fcn) ! rho
        call fcn%set_option('val',190.339029_rk)
        call initialize_variable(this%chidg%data,2,fcn) ! rhou
        call fcn%set_option('val',ZERO)
        call initialize_variable(this%chidg%data,3,fcn) ! rhov
        call fcn%set_option('val',ZERO)
        call initialize_variable(this%chidg%data,4,fcn) ! rhow
        call fcn%set_option('val',248493.425_rk)
        call initialize_variable(this%chidg%data,5,fcn) ! rhoE


        !
        ! Wrap-up initialization activities
        !
        call this%chidg%init('finalize')

    end subroutine SetUp
    !******************************************************************************************************


    subroutine tearDown(this)
        class(smoothbump),  intent(inout)   :: this

        ! Close ChiDG
        call this%chidg%close('core')

    end subroutine tearDown
    !******************************************************************************************************










    !>  Run the simulation.
    !!
    !!      - compute the entropy error, test against the reference values.
    !!      - store the nonlinear convergence to the module variable so it can 
    !!        be used by a test for nonlinear convergence.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !!
    !-------------------------------------------------------------------------------------------------------
    @Test(npes=[1,2,4,7,10])
    subroutine test_entropy_error_rate(self)
        class(smoothbump),  intent(inout)   :: self

        real(rk)    :: tol, entropy_error
        integer(ik) :: npes_index, grid_index, order_index


        npes_index  = get_npes_index(NRANK)
        grid_index  = get_grid_index(self%gridfile)
        order_index = get_order_index(self%order)


        !
        ! Run ChiDG simulation
        !
        call self%chidg%run()

        !
        ! Store the nonlinear convergence of the calculation
        !
        nonlinear_convergence(npes_index,grid_index,order_index) = self%chidg%nonlinear_solver%residual_norm

        !
        ! Compute entropy error
        !
        entropy_error = compute_entropy_error(self%chidg%data)

        !
        ! Test that computed entropy error is the expected value
        !
        tol = self%entropy_ref/100._rk
        @assertEqual(self%entropy_ref,entropy_error,tol)


    end subroutine test_entropy_error_rate
    !*******************************************************************************************************








    !>  Access the stored nonlinear convergence data(module global variable) and compare the parallel 
    !!  convergence to the serial convergence for each (grid,order) combination.
    !!
    !!  This assumes that the 'test_entropy_error_rate' has already been executed for all cases
    !!  and numbers of processors to that the 'nonlinear_convergence' variable has been populated.
    !!  So, we are relying on pFUnit to do things in order here. Works now, but I don't think
    !!  there is a guaruntee that this can't change.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !-------------------------------------------------------------------------------------------------------
    @Test(npes=[1])
    subroutine test_nonlinear_convergence(self)
        class(smoothbump),  intent(inout)   :: self

        integer(ik)             :: inpes, igrid, iorder
        real(rk), allocatable   :: reference_convergence(:), test_convergence(:)

        do inpes = 1,5
            do igrid = 1,3
                do iorder = 1,4

                    ! Get the serial convergence for this (igrid,iorder)
                    reference_convergence = nonlinear_convergence(1,igrid,iorder)%data()

                    ! Test that the current convergence for the processor count matches the serial convergence
                    test_convergence = nonlinear_convergence(inpes,igrid,iorder)%data()

                    @assertEqual(reference_convergence, test_convergence, 1.e-5_rk)

                end do
            end do
        end do


    end subroutine test_nonlinear_convergence
    !*******************************************************************************************************













    ! Utility for returning the index associated with a certain number of procs
    !------------------------------------------------------------------------------
    function get_npes_index(numproc) result(inpes)
        integer(ik) :: numproc
        integer(ik) :: inpes

        select case (numproc)
            case(1)
                inpes = 1
            case(2)
                inpes = 2
            case(4)
                inpes = 3
            case(7)
                inpes = 4
            case(10)
                inpes = 5

        end select

    end function get_npes_index
    !******************************************************************************



    ! Utility for returning the index associated with a certain grid file
    !------------------------------------------------------------------------------
    function get_grid_index(grid) result(igrid)
        character(len=*)    :: grid
        integer(ik)         :: igrid

        select case (trim(grid))
            case('smoothbump_6x2x2.h5')
                igrid = 1
            case('smoothbump_12x4x2.h5')
                igrid = 2 
            case('smoothbump_24x8x2.h5')
                igrid = 3
        end select

    end function get_grid_index
    !******************************************************************************



    ! Utility for returning the index associated with a certain order of accuracy
    !------------------------------------------------------------------------------
    function get_order_index(order) result(iorder)
        integer(ik) :: order
        integer(ik) :: iorder

        select case (order)
            case(1)
                iorder = 1
            case(2)
                iorder = 2
            case(3)
                iorder = 3
            case(4)
                iorder = 4
        end select

    end function get_order_index
    !******************************************************************************






end module integration_smoothbump_mod
