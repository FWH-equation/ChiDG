@Test
subroutine test_fgmres_solve()
    use pfunit_mod
    use mod_kinds,                  only: rk, ik
    use mod_constants,              only: ZERO, ONE, TWO, THREE, FOUR, DIAG, XI_MIN, XI_MAX, &
                                          IO_DESTINATION

    use type_chidg,                 only: chidg_t
    use type_point,                 only: point_t
    use type_domain_connectivity,   only: domain_connectivity_t

    use type_fgmres,                only: fgmres_t
    use precon_identity,            only: precon_identity_t

    use type_chidg_matrix
    use type_chidg_vector,           only: chidg_vector_t

    use mod_test_utilities,         only: create_mesh
    implicit none

    type(chidg_t)               :: chidg
    type(point_t),  allocatable :: nodes(:)
    type(domain_connectivity_t) :: connectivity

    real(rk)    :: tol
    integer(ik) :: nterms_c, idomain, spacedim, ixi_min, ixi_max, idiag, nelements_g, eqn_ID


    tol      = 1.e-9_rk
    idomain  = 1
    spacedim = 3
    nterms_c = 8
    IO_DESTINATION = 'file'


    !
    ! Initialize ChiDG
    !
    call chidg%start_up('core')



    !
    ! Set ChiDG components
    !
    call chidg%set('Time Integrator',  'steady'  )
    call chidg%set('Nonlinear Solver', 'newton'  )
    call chidg%set('Linear Solver',    'fgmres'  )
    call chidg%set('Preconditioner',   'identity')


    !
    ! Get grid points
    !
    call create_mesh('311',nodes,connectivity)



    !
    ! Initialize equation set
    !
    call chidg%data%add_equation_set('Scalar Advection')
    eqn_ID = chidg%data%get_equation_set_id('Scalar Advection')



    !
    ! Initialize domain
    !
    nelements_g = 3
    call chidg%data%mesh%add_domain('D_01',nodes,connectivity,nelements_g,spacedim,nterms_c,'Cartesian',eqn_ID)

    call chidg%set('Solution Order', integer_input=1)
    call chidg%init('domains')
    call chidg%init('communication')
    call chidg%init('solvers')




    associate ( A => chidg%data%sdata%lhs,  &
                b => chidg%data%sdata%rhs,  &
                x => chidg%data%sdata%dq,   &
                M => chidg%preconditioner )

        ! Initialize blockmatrix data (A)
        !
        ! [4  3  0]
        ! [3  4 -1]
        ! [0 -1  4]
        !
        idiag   = A%dom(1)%lblks(1,1)%loc(1,1)
        ixi_max = A%dom(1)%lblks(1,1)%loc(1,2)
        A%dom(1)%lblks(1,1)%data_(idiag)%mat   = FOUR
        A%dom(1)%lblks(1,1)%data_(ixi_max)%mat = THREE


        ixi_min = A%dom(1)%lblks(2,1)%loc(1,1)
        idiag   = A%dom(1)%lblks(2,1)%loc(1,2)
        ixi_max = A%dom(1)%lblks(2,1)%loc(1,3)
        A%dom(1)%lblks(2,1)%data_(ixi_min)%mat = THREE
        A%dom(1)%lblks(2,1)%data_(idiag)%mat   = 4._rk
        A%dom(1)%lblks(2,1)%data_(ixi_max)%mat = -ONE


        ixi_min = A%dom(1)%lblks(3,1)%loc(1,2)
        idiag   = A%dom(1)%lblks(3,1)%loc(1,3)
        A%dom(1)%lblks(3,1)%data_(ixi_min)%mat = -ONE
        A%dom(1)%lblks(3,1)%data_(idiag)%mat   = 4._rk




        ! Initialize blockvector data (b)
        !
        ! [ 24]
        ! [ 30]
        ! [-24]
        !
        b%dom(1)%vecs(1)%vec =  24._rk
        b%dom(1)%vecs(2)%vec =  30._rk
        b%dom(1)%vecs(3)%vec = -24._rk

        

        !
        ! Finalize ChiDG initialization
        !
        call chidg%init('finalize')


        !
        ! Solve matrix system. Ax=b. TESTING
        !
        chidg%linear_solver%report = .false.  
        chidg%linear_solver%tol    = 1.e-13_rk
        call chidg%linear_solver%solve(A,x,b,M)



        ! Expecting solution
        !
        !     [ 3]
        ! x = [ 4]
        !     [-5]
        !
        @assertEqual(3._rk,x%dom(1)%vecs(1)%vec,tol)
        @assertEqual(4._rk,x%dom(1)%vecs(2)%vec,tol)
        @assertEqual(-5._rk,x%dom(1)%vecs(3)%vec,tol)

    end associate


    !
    ! Close ChiDG interface
    !
    call chidg%shut_down('core')

end subroutine test_fgmres_solve
